COMMENT = { !"\\" ~ "%" ~ (!NEWLINE ~ ANY)* ~ NEWLINE? }

raw_file = @{ SOI ~ file ~ EOI }
file = !{ COMMENT? ~ atom* ~ COMMENT? }

atom = _{ command | text | escape_sequence | implicit_par | special_text | group | bracket_group }

command = _{ BACKSLASH ~ (
    include | chapter | sloppy | begin | omission | footnote | manuscriptit |
    mbox | end | cbreak | clearpage | fussy | textbackslash |
    unhandled_command
) }
    include = { "include" ~ group }
    chapter = { "chapter" ~ group }
    sloppy = { "sloppy" }
    begin = { "begin" ~ group }
    omission = { "omission" }
    footnote = { "footnote" ~ group }
    manuscriptit = { "manuscriptit" ~ group }
    mbox = { "mbox" ~ group }
    end = { "end" ~ group }
    cbreak = { "break" }
    clearpage = { "clearpage" }
    fussy = { "fussy" }
    textbackslash = { "textbackslash" }
    unhandled_command = { ASCII_ALPHA ~ ASCII_ALPHANUMERIC* ~ "*"? }
    BACKSLASH = _{ "\\" }
    IGNORE = _{ " " | "\t" | NEWLINE }

text = { (LETTER | NUMBER | SIMPLE_PUNCTUATION | SIMPLE_WHITESPACE)+ }
    SIMPLE_PUNCTUATION = _{ ";" | "," | "." | "?" | ":" | "!" }
    SIMPLE_WHITESPACE = _{ " " | "\t" }

escape_sequence = ${ "\\" ~ !ASCII_ALPHANUMERIC ~ ANY }

implicit_par = { NEWLINE ~ NEWLINE }

special_text = { special_sequence | SIMPLE_CHAR }
    special_sequence = _{ "---" | "--" | "``" | "''" }
    SIMPLE_CHAR = _{ !("&" | "%" | "$" | "#" | "_" | "{" | "}" | "^" | "\\" | "[" | "]") ~ ANY }

group = { "{" ~ atom* ~ "}" }
bracket_group = { "[" ~ atom* ~ "]" }

// TODO: better solution for arg/opt arg brace/bracket matching
