COMMENT = { !"\\" ~ "%" ~ (!NEWLINE ~ ANY)* ~ NEWLINE? }

raw_file = @{ SOI ~ file ~ EOI }
file = !{ COMMENT? ~ atom* ~ COMMENT? }

atom = _{ command | text | escape_sequence | implicit_par | special_text | group | bracket_group }

command = _{ BACKSLASH ~ (
    include | chapter | section | begin | end |
    mbox | textbackslash | omission | footnote | manuscriptit |
    cbreak | clearpage | fussy | sloppy | linebreak | vspace |
    unhandled_command
) }
    include = { "include" ~ group }
    chapter = { "chapter" ~ ignored_opt_group ~ group }
    section = { "section" ~ ignored_opt_group ~ group }
    begin = { "begin" ~ group }
    end = { "end" ~ group }
    mbox = { "mbox" ~ group }
    textbackslash = { "textbackslash" ~ IGNORE? }
    omission = { "omission" ~ IGNORE? }
    footnote = { "footnote" ~ group }
    manuscriptit = { "manuscriptit" ~ group }
    cbreak = { "break" ~ IGNORE? }
    clearpage = { "clearpage" ~ IGNORE? }
    fussy = { "fussy" ~ IGNORE? }
    sloppy = { "sloppy" ~ IGNORE? }
    linebreak = { BACKSLASH ~ IGNORE? }
    vspace = { "vspace" ~ "*" ~ group }
    unhandled_command = { ASCII_ALPHA ~ ASCII_ALPHANUMERIC* ~ "*"? }
    ignored_opt_group = _{ bracket_group? }
    BACKSLASH = _{ "\\" }
    IGNORE = _{ " " | "\t" | NEWLINE }

text = { (LETTER | NUMBER | SIMPLE_PUNCTUATION | SIMPLE_WHITESPACE)+ }
    SIMPLE_PUNCTUATION = _{ ";" | "," | "." | "?" | ":" | "!" }
    SIMPLE_WHITESPACE = _{ " " | "\t" }

escape_sequence = ${ "\\" ~ !("\\" | ASCII_ALPHANUMERIC) ~ ANY }

implicit_par = { NEWLINE ~ NEWLINE }

special_text = { special_sequence | SIMPLE_CHAR }
    special_sequence = _{ "---" | "--" | "``" | "''" }
    SIMPLE_CHAR = _{ !("&" | "%" | "$" | "#" | "_" | "{" | "}" | "^" | "\\" | "[" | "]") ~ ANY }

group = { "{" ~ atom* ~ "}" }
bracket_group = { "[" ~ atom* ~ "]" }

// TODO: better solution for arg/opt arg brace/bracket matching
